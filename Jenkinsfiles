pipeline {
    agent any

    environment {
        SERVICES = "spring-petclinic-vets-service,spring-petclinic-customers-service,spring-petclinic-visits-service,spring-petclinic-admin-server,spring-petclinic-api-gateway,spring-petclinic-config-server,spring-petclinic-genai-service,spring-petclinic-discovery-server"
    }

    stages {
        stage('Detect Changed Services') {
            steps {
                script {
                    def changedFiles = sh(script: "git diff --name-only HEAD~1", returnStdout: true).trim().split("\n")
                    def affectedServices = []

                    SERVICES.split(',').each { service ->
                        if (changedFiles.any { it.startsWith(service + "/") }) {
                            affectedServices.add(service)
                        }
                    }

                    if (affectedServices.isEmpty()) {
                        echo "No services changed. Skipping build."
                        currentBuild.result = 'SUCCESS'
                        // Set env to avoid executing later stage
                        env.BUILD_SERVICES = ''
                        return
                    }

                    env.BUILD_SERVICES = affectedServices.join(',')
                    echo "Changed services: ${env.BUILD_SERVICES}"
                }
            }
        }

        stage('Login to Docker Hub') {
            when {
                expression { return env.BUILD_SERVICES?.trim() }
            }
            steps {
                withCredentials([usernamePassword(credentialsId: 'docker_hub', usernameVariable: 'DOCKER_USER', passwordVariable: 'DOCKER_PASS')]) {
                    sh 'echo "$DOCKER_PASS" | docker login -u "$DOCKER_USER" --password-stdin'
                }
            }
        }

        stage('Build & Push Docker Images') {
            when {
                expression { return env.BUILD_SERVICES?.trim() }
            }
            steps {
                script {
                    def commitId = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                    env.COMMIT_ID = commitId

                    sh './mvnw clean install -PbuildDocker'

                    env.BUILD_SERVICES.split(',').each { service ->
                        def serviceName = service.replaceFirst('spring-petclinic-', '')
                        echo "Tagging and pushing ${service}..."

                        // Assuming Docker image is built as springframeworkpetclinic/<service-name>
                        def imageName = "springcommunity/${service}"

                        sh """
                            docker tag ${imageName} trgtamthanh/${serviceName}:${commitId}
                            docker push trgtamthanh/${serviceName}:${commitId}
                        """
                    }
                }
            }
        }
    }
}
